buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.0.0"
group = "thaumicenergistics"
archivesBaseName = "thaumicenergistics"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20171003"
}

repositories {
    maven {
        name = "progwml6"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "covers1624"
        url = "http://maven.covers1624.net"
    }
    // CF Maven always last
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile "baubles:baubles:${version_ba}"
    deobfCompile "thaumcraft:thaumcraft:${version_tc}"
    deobfCompile("appeng:appliedenergistics2:${version_ae2}") {
        transitive = false
    }
    // Testing mods
    runtime "mezz.jei:jei_${version_jei}"
    runtime "cofh:RedstoneFlux:${version_rf}:universal"
    runtime "p455w0rd:p455w0rdslib:${version_pwlib}"
    runtime "p455w0rd:AE2WTLib:${version_wtlib}"
    runtime "p455w0rd:WirelessCraftingTerminal:${version_wtlib}"
}

jar {
    manifest {
        attributes 'FMLAT': 'theeng_at.cfg'
    }
    from sourceSets.api.output
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    from sourceSets.api.java
    classifier = "api"
}

task deobfJar(type: Jar, dependsOn: 'jar') {
    from "build/sources/main/java"
    from "build/resources/main"
    classifier "dev"
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from "build/docs/javadoc"
    classifier "javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.api.java
    from sourceSets.main.java
    classifier = 'sources'
}

sourceSets {
    api
    main
}

artifacts {
    archives apiJar
    archives deobfJar
    archives sourcesJar
    archives javadocJar
}

// TODO: Publish in a maven repo

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}
